000100 IDENTIFICATION DIVISION.
000200  PROGRAM-ID. LOGANALYZER.
000300  AUTHOR. ELIAS GAUTHIER.
000400  DATE-WRITTEN. 19/03/2025
000500  DATE-COMPILED. 19/02/2025.
000600*
000700 ENVIRONMENT DIVISION.
000800  CONFIGURATION SECTION.
000900   SOURCE-COMPUTER. LINUX.
001000   OBJECT-COMPUTER. LINUX.
001100  INPUT-OUTPUT SECTION.
001200   FILE-CONTROL.
001300    SELECT LOG-FILE ASSIGN TO DYNAMIC FILENAME
001400     ORGANIZATION IS LINE SEQUENTIAL
001500     FILE STATUS IS WS-FILE-STATUS.
001600*
001700 DATA DIVISION.
001800  FILE SECTION.
001900  FD LOG-FILE.
002000   01 LOG-RECORD PIC X(1024).
002100*
002200  WORKING-STORAGE SECTION.
002300   01 WS-FILE-STATUS    PIC XX VALUE SPACES.
002400   01 FILENAME          PIC X(256) VALUE SPACES.
002500   01 WS-EOF            PIC X VALUE 'N'.
002600    88 END-OF-FILE      VALUE 'Y'.
002700    88 NOT-END-OF-FILE  VALUE 'N'.
002800   01 WS-LINE-COUNTER   PIC 9(10) VALUE ZEROES.
002900   01 WS-ERROR-MESSAGE  PIC X(100) VALUE SPACES.
003000   01 WS-DISPLAY-MODE   PIC X VALUE 'P'.
003100    88 DISPLAY-ALL      VALUE 'A'.
003200    88 DISPLAY-PARTIAL  VALUE 'P'.
003300    88 DISPLAY-NONE     VALUE 'N'.
003400   01 WS-DISPLAY-INTERVAL PIC 9(9) VALUE 10000.
003500   01 WS-USER-OPTION    PIC X VALUE SPACE.
003600   01 WS-START-TIME     PIC 9(8) VALUE ZEROES.
003700   01 WS-END-TIME       PIC 9(8) VALUE ZEROES.
003800   01 WS-ELAPSED-TIME   PIC 9(8) VALUE ZEROES.
003900   01 WS-DEBUG-INFO     PIC X(100).
004000   01 WS-LINE-LENGTH    PIC 9(4).
004100*
004200 PROCEDURE DIVISION.
004300 MAIN-PROCEDURE.
004400    DISPLAY "LOG FILE ANALYZER - V-0.1 - E. GAUTHIER"
004410    DISPLAY "This program is still under developpement."
004500    DISPLAY "====================================="
004600    DISPLAY "Enter log file path: " 
004700    ACCEPT FILENAME
004800    
004900    DISPLAY "Select display mode:"
005000    DISPLAY "A - Display all lines"
005100    DISPLAY "P - Display progress periodically (default)"
005200    DISPLAY "N - Display only summary"
005300    DISPLAY "Your choice (A/P/N): "
005400    ACCEPT WS-USER-OPTION
005500    
005600    EVALUATE WS-USER-OPTION
005700       WHEN 'A' WHEN 'a'
005800          SET DISPLAY-ALL TO TRUE
005900       WHEN 'N' WHEN 'n'
006000          SET DISPLAY-NONE TO TRUE
006100       WHEN OTHER
006200          SET DISPLAY-PARTIAL TO TRUE
006300    END-EVALUATE
006400    
006500    IF DISPLAY-PARTIAL
006600       DISPLAY "Show progress after how many lines?"
006700       ACCEPT WS-DISPLAY-INTERVAL
006800    END-IF
006900    
007000    ACCEPT WS-START-TIME FROM TIME
007100    PERFORM OPEN-FILE
007200    
007300    IF WS-FILE-STATUS = "00"
007400       PERFORM READ-LOG-FILE-LOOP
007500       PERFORM CLOSE-FILE
007600       ACCEPT WS-END-TIME FROM TIME
007700       COMPUTE WS-ELAPSED-TIME = WS-END-TIME - WS-START-TIME
007800       DISPLAY "---------------------------------"
007900       DISPLAY "SUMMARY:"
008000       DISPLAY "Total lines read: " WS-LINE-COUNTER
008100       DISPLAY "Processing time: " WS-ELAPSED-TIME 
008200          " hundredths of a second"
008300    END-IF
008400    
008500    STOP RUN.
008600*
008700 OPEN-FILE.
008800    OPEN INPUT LOG-FILE
008900    EVALUATE WS-FILE-STATUS
009000       WHEN "00"
009100          DISPLAY "File opened successfully."
009200          DISPLAY "File path: " FUNCTION TRIM(FILENAME)
009300       WHEN "35"
009400          DISPLAY "File does not exist."
009500       WHEN OTHER
009600          STRING "Error opening file. Status: " DELIMITED BY SIZE
009700                 WS-FILE-STATUS DELIMITED BY SIZE
009800                 INTO WS-ERROR-MESSAGE
009900          DISPLAY WS-ERROR-MESSAGE
010000    END-EVALUATE.
010100*
010310 READ-LOG-FILE-LOOP.
010400    PERFORM UNTIL END-OF-FILE
010500       READ LOG-FILE
010600          AT END 
010700             SET END-OF-FILE TO TRUE
010800             DISPLAY "End of file reached."
010900          NOT AT END
011000             ADD 1 TO WS-LINE-COUNTER
011100             
011200             COMPUTE WS-LINE-LENGTH = FUNCTION LENGTH(
011300                                    FUNCTION TRIM(LOG-RECORD))

011400             STRING "Line " WS-LINE-COUNTER 
011500                    " - Status: " WS-FILE-STATUS
011600                    " - Length: " WS-LINE-LENGTH
011700                    DELIMITED BY SIZE
011800                    INTO WS-DEBUG-INFO
011900             
012000             IF DISPLAY-ALL
012100                DISPLAY WS-DEBUG-INFO
012200                DISPLAY WS-LINE-COUNTER ": " 
012300                   FUNCTION TRIM(LOG-RECORD)
012400             END-IF
012500             
012600             IF DISPLAY-PARTIAL AND
012700                FUNCTION MOD(WS-LINE-COUNTER,
012800                   WS-DISPLAY-INTERVAL) = 0
012900                DISPLAY "Processed " WS-LINE-COUNTER " lines..."
013000             END-IF
013100       END-READ
013200       
013300
013400       IF WS-FILE-STATUS NOT = "00" AND 
013500          WS-FILE-STATUS NOT = "10"
013600          DISPLAY "Error during read - Status: " WS-FILE-STATUS
013700          SET END-OF-FILE TO TRUE
013800       END-IF
013900    END-PERFORM.
014000*
014100 CLOSE-FILE.
014200    CLOSE LOG-FILE
014300    DISPLAY "File closed. Status: " WS-FILE-STATUS.
